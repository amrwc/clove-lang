plugins {
    id 'application'
    id 'com.intershop.gradle.javacc' version '4.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'com.google.guava:guava:31.0.1-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'

    compileOnly 'net.java.dev.javacc:javacc:7.0.10'
}

application {
    mainClass = 'dev.amrw.clovelang.CloveMain'
}

run {
    // Let `stdin` through.
    standardInput = System.in
}

javacc {
    configs {
        javaCCVersion = '7.0.10'
        main {
            staticParam = 'false'
            jjtree {
                inputFile = file('src/main/java/dev/amrw/clovelang/parser/Clove.jjt')
                outputDir = file('src/main/java/dev/amrw/clovelang/parser/ast')
            }
        }
    }

    clean {
        delete file('src/main/java/dev/amrw/clovelang/parser/ast')
    }
}

test {
    useJUnitPlatform {
        excludeTags 'integration-test'
    }
    exclude 'dev/amrw/clovelang/integration/**'
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'integration-test'
    }
    include 'dev/amrw/clovelang/integration/**'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        // Display `unchecked` warnings
        options.compilerArgs << '-Xlint:unchecked'
    }
}
